#!/usr/bin/env zsh
set -ue

cd "$(tmux showenv "$(tmux display -p "TMUX_PWD_#D")" | cut -d= -f2-)"

format() {
  printf "#[fg=%s,bg=%s] %s " "$2" "$3" "$1"
}

git_trusted() {
  if git trusted; then
    printf "^"
  fi
}

git_status() {
  local s="$(git status)"
  if egrep "^# Untracked|^# Changed|^# Changes not" > /dev/null <<< "$s"; then
    printf "?"
  fi
  if grep "^# Changes to" > /dev/null <<< "$s"; then
    printf "\!"
  fi
  printf ' ' # not using format, so give it some space
}

git_since() {
  local seconds="$(git since)"
  if [[ "$seconds" -gt 3600 ]]; then
    format "$((seconds / 3600))h$((seconds % 3600 / 60))m" "black" "red"
  elif [[ "$seconds" -gt 60 ]]; then
    format "$((seconds / 60))m" "black" "green"
  else
    format "<1m" "black" "green"
  fi
}

git_branch() {
  format "$(git rev-parse --abbrev-ref HEAD)" "black" "white"
}

short_pwd() {
  format "$(pwd | sed "s#^$HOME#~#; s#\([a-zA-Z]\)[a-zA-Z]*[^/]*/#\1/#g")" "black" "blue"
}

if git root &> /dev/null; then
  git_trusted
  git_status
  git_since
  git_branch
fi

short_pwd
