#!/usr/bin/env fish

function change_to_tmux_pwd
  set -l var_name (tmux display -p "TMUX_PWD_#D")
  set -l tmux_pwd (tmux showenv $var_name | cut -d = -f 2-)
  cd $tmux_pwd
end

function short_pwd
  pwd | sed "s#^$HOME#~#; s#\([a-zA-Z]\)[a-zA-Z]*[^/]*/#\1/#g"
end

function git_status
  set -l staged (git diff-index --cached HEAD | wc -l)
  set -l unstaged (git diff-files | wc -l)
  if test $staged -gt 0
    printf " #[fg=colour2]%dstgd " "$staged"
  end
  if test $unstaged -gt 0
    printf " #[fg=colour3]%dwait " "$unstaged"
  end
end

function git_since
  set -l seconds (git since)
  if test $seconds -gt 1800 # >= 30 mins is too long
    printf " #[fg=colour1]>30mins "
  else if test $seconds -gt 60 # >= 1 minute is ok
    printf " #[fg=colour9]%dmins " (math $seconds/60)
  else # < 1 minute is super nice
    printf " #[fg=colour2]<1min "
  end
end

function git_branch
  printf " #[fg=colour7]%s " (git rev-parse --abbrev-ref HEAD)
end

function git_pwd
  if git trusted
    printf " #[fg=colour9]%s" (short_pwd)
  else
    printf " #[fg=colour4]%s" (short_pwd)
  end
end

function statusline
  if git root > /dev/null ^&1
    git_status
    git_since
    git_branch
    git_pwd
  else
    printf " #[fg=colour4]%s " (short_pwd)
  end
end

change_to_tmux_pwd
statusline
