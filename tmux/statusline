#!/usr/bin/env fish

function change_to_tmux_pwd
  set -l var_name (tmux display -p "TMUX_PWD_#D")
  set -l tmux_pwd (tmux showenv $var_name | cut -d = -f 2-)
  cd $tmux_pwd
end

function short_pwd
  pwd | sed "s#^$HOME#~#; s#\([a-zA-Z]\)[a-zA-Z]*[^/]*/#\1/#g"
end

function git_status
  set -l unstaged (git diff-files | wc -l)
  set -l staged (git diff-index --cached HEAD | wc -l)
  if test $unstaged -gt 0
    printf " #[fg=colour3]%d waiting " "$unstaged"
  end
  if test $staged -gt 0
    printf " #[fg=colour1]%d staged " "$staged"
  end
end

function git_since
  set -l seconds (git since)
  if test $seconds -gt 3600
    printf " #[fg=colour9]%dh%dm " (math "$seconds / 3600") (math "$seconds % 3600 / 60")
  else if test $seconds -gt 60
    printf " #[fg=colour2]%dm " (math "$seconds / 60")
  else
    printf " #[fg=colour2]<1m "
  end
end

function git_branch
  printf " #[fg=colour7]%s " (git rev-parse --abbrev-ref HEAD)
end

function git_pwd
  if git trusted
    printf " #[fg=colour9]%s" (short_pwd)
  else
    printf " #[fg=colour4]%s" (short_pwd)
  end
end

function statusline
  if git root > /dev/null
    git_status
    git_since
    git_branch
    git_pwd
  else
    printf " #[fg=color4]%s " (short_pwd)
  end
end

change_to_tmux_pwd
statusline
